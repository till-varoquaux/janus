// -*- tuareg -*-
%{
 open Pos
%}


%token <string> Ident
%token <AstBase.constant> Constant

/* Keywords */

%token If Else While For And Not Var Mod EOF
%token Function Or  Return Cps T
%token CallCC Throw Block

/* Symbols */

%token Lpar Rpar
%token Lsquare Rsquare
%token Lbracket Rbracket
%token Semicolon Comma
%token Colon
%token Arrow DoubleArrow Dot

/* Operators  */

%token Aff // :=
%token Eq // =
%token <AstStd.binop> Comp
%token Plus Minus
%token Star Slash

/* Precedence  */
%nonassoc If
%nonassoc Else

%left Comp Eq
%left Plus Minus Or
%left Star Slash Mod And
%nonassoc uminus
%nonassoc unot

/* Entry point  */
%start program
%type <AstStd.program> program

%%

tyd:
| Colon ty {$2}
| {`T}

arg:
| ident tyd {($1,$2)}

args:
| {[]}
| arg args2 {$1::$2}

args2:
| {[]}
| Comma arg args2 {$2::$3}

/*Fields in an object declaration*/
field:
 | ident Colon expr {($1,$3)}

fields:
| {[]}
| field fields2 {$1::$2}

fields2:
| {[]}
| Comma field fields2 {$2::$3}

expr1:/*can be the LHS of a function call*/
| fundecl Lpar args Rpar tyd bloc {
   let args,args_ty= List.split $3 in
   let f=`Fun (args,$6) in
   let ty=(if $1 then
            `CpsArrow (args_ty,$5)
           else
            `Arrow (args_ty,$5)
          )
   in
   `Typed(f,ty)}
| lvalue {AstStd.lval2expr $1}
| expr2 {$1}

expr2:/*can be the LHS of an array access or an object access*/
| Lpar expr Rpar {$2}
| Lsquare exprs Rsquare {`Array $2}
| expr2 Dot ident {`ObjAccess($1,$3)}
| expr2 Lsquare expr Rsquare {`ArrayAccess ($1,$3)}
| CallCC Lpar expr Rpar {`CallCC($3,[])}
| CallCC Lpar expr Comma exprs Rpar {`CallCC($3,$5)}
| Block Lpar expr Rpar {`BlockingEv($3,[])}
| Block Lpar expr Comma exprs Rpar {`BlockingEv($3,$5)}
| expr1 Lpar exprs Rpar {`Call ($1,$3)}

expr_aux:
| Not expr %prec unot {`Unop (`Not,$2)}
| Minus expr %prec uminus {`Unop (`Minus,$2)}
| expr Eq Eq expr {`Binop (`Eq,$1, $4)}
| expr Comp expr {`Binop ($2, $1, $3)}
| expr Plus expr {`Binop (`Add, $1, $3)}
| expr Minus expr {`Binop (`Sub, $1, $3)}
| expr Star expr {`Binop (`Mul, $1, $3)}
| expr Slash expr {`Binop (`Div, $1, $3)}
| expr Mod expr {`Binop (`Mod, $1, $3)}
| expr And expr {`Binop (`And, $1, $3)}
| expr Or expr {`Binop (`Or, $1, $3)}
| Constant {`Cst $1}
| Lbracket fields Rbracket {`Obj $2}
| expr1 {$1}

fundecl:
| Function {false}
| Cps {true}

expr:
| expr_aux {`Pos ((symbol_start_pos(),symbol_end_pos()),$1)}

exprs:
| {[]}
| expr exprs2 {$1::$2}

exprs2:
| {[]}
| Comma expr exprs2 {$2::$3}

maybeExpr:
| expr {Some $1}
| {None}

lvalue:
| ident {`Ident $1}
| lvalue Lsquare expr Rsquare {`ArrayAccess($1,$3)}
| lvalue Dot ident {`ObjAccess($1,$3)}

cond:
| Lpar expr Rpar {$2}

instr:
| instr_aux {`Pos ((symbol_start_pos(),symbol_end_pos()),$1)}

instr_aux:
| Var ident Eq expr {`Var ($2,Some $4)}
| lvalue Eq expr {`Assign ($1,$3)}
| lvalue Minus Minus {`Assign ($1,`Binop (`Sub,(AstStd.lval2expr $1),`Cst (`Int 1)))}
| lvalue Plus Plus {`Assign ($1,`Binop (`Add,(AstStd.lval2expr $1),`Cst (`Int 1)))}
| fundecl ident Lpar args Rpar tyd bloc {
   let args,args_ty= List.split $4 in
   let f=`Fun (args,$7) in
   let ty=(if $1 then
            `CpsArrow (args_ty,$6)
           else
            `Arrow (args_ty,$6)
          )
   in
   `Var ($2, Some(`Typed(f,ty)))}
| If cond bloc_or_instr {`If ($2,$3,`Bloc [])}
| If cond bloc_or_instr Else bloc_or_instr {`If ($2,$3,$5)}
| While cond bloc_or_instr {`While ($2,$3)}
| For Lpar maybeInstr Semicolon maybeExpr Semicolon maybeInstr Rpar
    bloc_or_instr
    {let cond=match $5 with
      | Some e -> e
      | None -> `Cst(`Bool true)
     and incr = match $7 with
      | Some i -> i
      | None -> `Bloc []
     and init = match $3 with
      | Some i -> i
      | None -> `Bloc []
     in
     `Bloc [
      init;
      `While(cond,`Bloc[
              $9;
              incr])]
    }
| Return expr {`Ret (Some $2)}
| Return {`Ret None}
| CallCC Lpar expr Rpar {`CallCC($3,[])}
| CallCC Lpar expr Comma exprs Rpar {`CallCC($3,$5)}
| Block Lpar expr Rpar {`BlockingEv($3,[])}
| Block Lpar expr Comma exprs Rpar {`BlockingEv($3,$5)}
| Throw Lpar expr Comma expr Rpar {`Throw($3,$5)}
| expr1 Lpar exprs Rpar {`Call($1,$3)}

maybeInstr:
| instr {Some $1}
| {None}

ty:
| T {`T}
| Lpar tys Rpar Arrow ty {`Arrow ($2,$5)}
| Lpar tys Rpar DoubleArrow ty {`CpsArrow ($2,$5)}


tys:
| ty tys2 {$1::$2}
| {[]}

tys2:
| Comma ty tys2 {$2::$3}
| {[]}

bloc:
| Lbracket instrs Rbracket {`Bloc $2}

bloc_or_instr:
| bloc {$1}
| instr {$1}

instrs:
| {[]}
| instr Semicolon instrs {$1::$3}
| instr {[$1]}

ident:
| Ident {{node = $1 ; loc=symbol_start_pos(),symbol_end_pos()}}

program:
| instrs {$1}
